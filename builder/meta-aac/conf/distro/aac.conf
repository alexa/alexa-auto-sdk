DISTRO = "aac"
DISTRO_NAME = "Alexa Auto SDK"
DISTRO_VERSION ?= "1.0.0"
DISTRO_CODENAME ?= "elderberry"

# Workarounds
USER_CLASSES = ""

# Use all cross-toolchain from host
ASSUME_PROVIDED += "\
    linux-libc-headers \
    virtual/${TARGET_PREFIX}gcc \
    virtual/${TARGET_PREFIX}binutils \
    virtual/${TARGET_PREFIX}libc-for-gcc \
    virtual/${TARGET_PREFIX}compilerlibs \
    virtual/libc \
    virtual/libiconv \
    "

# Our dedicated root path prefix
export prefix = "/opt/AAC"

# Temporary location for release contents
releasedir = "${datadir}/aac_release"
FILES_${PN} += "${releasedir}"

# Skip these QA for now
ERROR_QA_remove = "file-rdeps"
WARN_QA_remove = "useless-rpaths host-user-contaminated"

# Use our pkgconfig logic
INHERIT += "aac-base"

# Use AVS Device SDK v1.7
PREFERRED_VERSION_avs-device-sdk ?= "1.7%"

# Debug build
OECMAKE_BUILD_TYPE = "${@['RELEASE', 'DEBUG'][d.getVar('DEBUG_BUILD') == '1']}"
IMAGE_GEN_DEBUGFS ?= "${@['0', '1'][d.getVar('DEBUG_BUILD') == '1']}"
PACKAGE_DEBUG_SPLIT_STYLE = "debug-without-src"
AAC_SENSITIVE_LOGS ?= "OFF"
AAC_LATENCY_LOGS ?= "OFF"

##################################################################
# Amazon Build Info
##################################################################

AMAZON_SOURCES ?= "${HOME}/amazon_sources"

PACKAGE_MACHINE ?= "${@[d.getVar('BUILD_SYS'), d.getVar('MACHINE')][d.getVar('MACHINE') != 'native']}"
