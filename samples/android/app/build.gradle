apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.amazon.sampleapp"
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 1
        versionName "2.1"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "type"
    productFlavors {
        remote {
            dimension "type"
        }
        local {
            dimension "type"
        }
    }
    // Prevent duplicate .so libraries
    packagingOptions {
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'lib/x86/libc++_shared.so'
        pickFirst 'lib/x86_64/libc++_shared.so'
    }
    // Enable desugaring (dependency contains Java 8 bytecode)
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

ext {
    remoteLibVersion = "2.1+"
}

dependencies {
    implementation fileTree(include: ['*.jar','*.aar'], dir: 'src/main/libs')
    gradle.startParameter.getTaskNames().each { task ->
        if(task.toLowerCase().contains("local")) {
            localImplementation fileTree(include: ['*.jar','*.aar'], dir: "${project.rootDir}/../../builder/deploy/aar")
        }
    }
    gradle.startParameter.getTaskNames().each { task ->
        if(task.toLowerCase().contains("remote")) {
            remoteImplementation "com.amazon.alexa.aace:core:${remoteLibVersion}@aar"
            remoteImplementation "com.amazon.alexa.aace:alexa:${remoteLibVersion}@aar"
            remoteImplementation "com.amazon.alexa.aace:addressbook:${remoteLibVersion}@aar"
            remoteImplementation "com.amazon.alexa.aace:apl:${remoteLibVersion}@aar"
            remoteImplementation "com.amazon.alexa.aace:cbl:${remoteLibVersion}@aar"
            remoteImplementation "com.amazon.alexa.aace:contactuploader:${remoteLibVersion}@aar"
            remoteImplementation "com.amazon.alexa.aace:car-control:${remoteLibVersion}@aar"
            remoteImplementation "com.amazon.alexa.aace:maccandroid:${remoteLibVersion}@aar"
            remoteImplementation "com.amazon.alexa.aace:navigation:${remoteLibVersion}@aar"
            remoteImplementation "com.amazon.alexa.aace:phonecontrol:${remoteLibVersion}@aar"
            remoteImplementation "com.amazon.alexa.aace:sample-core:${remoteLibVersion}@aar"
        }
    }
    implementation 'com.android.support:appcompat-v7:28+'
    implementation 'com.android.support:design:28+'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:cardview-v7:28+'
    implementation 'com.android.support:recyclerview-v7:28+'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.7.1'
    implementation 'com.google.android.exoplayer:exoplayer-dash:2.7.1'
    implementation 'com.google.android.exoplayer:exoplayer-smoothstreaming:2.7.1'
    implementation 'com.google.android.exoplayer:exoplayer-hls:2.7.1'
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    implementation 'com.github.bumptech.glide:glide:4.6.1'

}
