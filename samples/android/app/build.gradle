apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.amazon.sampleapp"
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 1
        versionName "3.3"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "type"
    productFlavors {
        remote {
            dimension "type"
        }
        local {
            dimension "type"
        }
    }
    // Prevent duplicate .so libraries
    packagingOptions {
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'lib/x86/libc++_shared.so'
        pickFirst 'lib/x86_64/libc++_shared.so'
        doNotStrip '**/*'
    }
    // Enable desugaring (dependency contains Java 8 bytecode)
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

ext {
    remoteLibVersion = "3.3+"
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'src/main/libs')

    def tasks = gradle.startParameter.getTaskNames()
    if (tasks.empty || tasks.any { it.toLowerCase().contains("local") }) {
        localImplementation fileTree(include: ['*.jar', '*.aar'], dir: "${project.rootDir}/../../builder/deploy/aar")
    } else if (tasks.any { it.toLowerCase().contains("remote") }) {
        remoteImplementation "com.amazon.alexa.aace:core:${remoteLibVersion}@aar"
        remoteImplementation "com.amazon.alexa.aace:alexa:${remoteLibVersion}@aar"
        remoteImplementation "com.amazon.alexa.aace:addressbook:${remoteLibVersion}@aar"
        remoteImplementation "com.amazon.alexa.aace:apl:${remoteLibVersion}@aar"
        remoteImplementation "com.amazon.alexa.aace:cbl:${remoteLibVersion}@aar"
        remoteImplementation "com.amazon.alexa.aace:car-control:${remoteLibVersion}@aar"
        remoteImplementation "com.amazon.alexa.aace:connectivity:${remoteLibVersion}@aar"
        remoteImplementation "com.amazon.alexa.aace:maccandroid:${remoteLibVersion}@aar"
        remoteImplementation "com.amazon.alexa.aace:messaging:${remoteLibVersion}@aar"
        remoteImplementation "com.amazon.alexa.aace:navigation:${remoteLibVersion}@aar"
        remoteImplementation "com.amazon.alexa.aace:phonecontrol:${remoteLibVersion}@aar"
        remoteImplementation "com.amazon.alexa.aace:sample-core:${remoteLibVersion}@aar"
        remoteImplementation "com.amazon.alexa.aace:sample-connectivity:${remoteLibVersion}@aar"
        remoteImplementation "com.amazon.alexa.aace:text-to-speech:${remoteLibVersion}@aar"
        remoteImplementation "com.amazon.alexa.aace:text-to-speech-provider:${remoteLibVersion}@aar"
    }

    implementation 'androidx.media:media:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.7.1'
    implementation 'com.google.android.exoplayer:exoplayer-dash:2.7.1'
    implementation 'com.google.android.exoplayer:exoplayer-smoothstreaming:2.7.1'
    implementation 'com.google.android.exoplayer:exoplayer-hls:2.7.1'
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    implementation 'com.github.bumptech.glide:glide:4.11.0'

    implementation 'com.google.dagger:dagger-android:2.33'
    implementation 'com.google.dagger:dagger-android-support:2.33'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.33'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.33'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'

}
